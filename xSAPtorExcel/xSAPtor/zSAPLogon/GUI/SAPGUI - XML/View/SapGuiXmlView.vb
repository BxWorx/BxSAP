Imports System.Threading
Imports System.Windows.Forms

Imports xSAPtorExcel.Utilities
Imports xSAPtorExcel.Utilities.MsgHub
'••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••••
Namespace Main.SAPLogon

	Friend Class SapGuiXmlView

		#Region "Definitions"

			Private	co_Context			As SynchronizationContext
			Private ct_NodeList			As List(Of TreeNode)

		#End Region
		'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
		#Region "Methods: Exposed"
		#End Region
		'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
		#Region "Constructor/Destruction: Methods & Events"

			Private	co_SubMvvMCmd		As	iSubscription(Of mMvvMMsg)
			'¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨
			Friend Sub New()

				InitializeComponent()
				'..................................................
				Me.co_Context			= SynchronizationContext.Current
				Me.ct_NodeList		= New List(Of TreeNode)
				'..................................................
				Me.co_SubMvvMCmd	= so_MsgHub.Value.Subscribe(Of mMvvMMsg)	(AddressOf Me.mh_VMCmds)
				
			End Sub

		#End Region
		'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
		#Region "Methods: Message Handling"

			'¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨
			Private Sub mh_VMCmds(ByVal _cmd	As mMvvMMsg)

				Select Case	_cmd.Command

					Case MvvMCmd.Refresh		:	mh_RefreshTree(_cmd)
					Case MvvMCmd.CloseView	:	mh_CloseView(_cmd)

					'Case	VVMCommands.ProgressTask	: Me.mh_UpdateProgress(_cmd)
					'Case	VVMCommands.Treechecked			: Me.mh_TreeChecked()

				End Select

			End Sub
			'¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨
			Private Sub mh_RefreshTree(ByVal _cmd	As mMvvMMsg)

				Me.ct_NodeList.Clear()
				Me.ct_NodeList.AddRange(CType(_cmd.Payload, List(Of TreeNode)))
				Me.Update_Tree()

			End Sub
			'¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨
			Private Sub mh_CloseView(ByVal _cmd	As mMvvMMsg)

				Me.co_Context.Post(	Sub() Me.Close(),	Nothing )

			End Sub

		#End Region
		'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
		#Region "Methods: Private"

			'¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨
			Private Sub mh_Load() Handles xbtn_ts_ResetSel.Click

				Dim lt_Nodes	= Me.GetCheckedNodes()
				If lt_Nodes.Count.Equals(0)	Then	Exit Sub
				'..................................................
				so_MsgHub.Value.Publish(	New mMvvMMsg(	MvvMCmd.Data,	lt_Nodes )	)

			End Sub
			'¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨
			Private Function GetCheckedNodes()	_
												As List(Of TreeNode)

				Dim lt_Nodes	= New List(Of TreeNode)
				'..................................................
				lt_Nodes.AddRange(
					Me.xtvw_SAPSystems.FlattenTree().Where(
						Function(lo_Node)

							Dim lb_Ret	As Boolean	= False

							If lo_Node.Nodes.Count.Equals(0)
								If lo_Node.Checked
									lb_Ret	=	True
								End If
							End If
							'..........................................
							Return	lb_Ret

						End Function).ToList()
					)
				'..................................................
				Return lt_Nodes

			End Function
			'¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨
			Private Sub Update_Tree()

				Me.co_Context.Post(
					Sub()

						Me.xtvw_SAPSystems.BeginUpdate()
						Me.xtvw_SAPSystems.Nodes.Clear()
						Me.xtvw_SAPSystems.Nodes.AddRange(Me.ct_NodeList.ToArray)
						Me.xtvw_SAPSystems.EndUpdate()

					End Sub,	Nothing )

			End Sub
			'¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨
			Private Sub TreeCheckRecursive(ByVal _node As TreeNode)

				For Each lo_Node As TreeNode	In _node.Nodes

					lo_Node.Checked	= _node.Checked

					If lo_Node.GetNodeCount(False) > 0
						Me.TreeCheckRecursive(lo_Node)
					End If

				Next

			End Sub

		#End Region
		'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
		#Region "WB/WS Tree Control"

			'¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨¨
			Private Sub xtvw_SAPSystems_AfterCheck(sender As Object,
																						 e			As TreeViewEventArgs) _
										Handles xtvw_SAPSystems.AfterCheck

				If Not e.Action.Equals(TreeViewAction.ByKeyboard) AndAlso
					 Not e.Action.Equals(TreeViewAction.ByMouse)		Then	Exit Sub
				'..................................................
				RemoveHandler	Me.xtvw_SAPSystems.AfterCheck,
											AddressOf Me.xtvw_SAPSystems_AfterCheck

				Me.TreeCheckRecursive(e.Node)	

				AddHandler	Me.xtvw_SAPSystems.AfterCheck,
										AddressOf Me.xtvw_SAPSystems_AfterCheck

			End Sub

		#End Region

	End Class

End Namespace
